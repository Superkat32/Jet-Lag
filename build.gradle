plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id "dev.kikugie.stonecutter"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	accessWidenerPath = getRootProject().file("src/main/resources/jetlag.accesswidener")

	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and just create separate worlds
	}
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url = "https://api.modrinth.com/maven"}
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}

	//Do A Barrel Roll
	maven {
		url "https://maven.enjarai.nl/releases"
	}
	maven {
		url "https://maven.enjarai.nl/mirrors"
	}

	//dabr dep
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//Mod Menu
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

	//YACL
	modImplementation "dev.isxander:yet-another-config-lib:${yacl_version}"

	//Do A Barrel Roll and its API
	modImplementation("nl.enjarai:do-a-barrel-roll:${project.dabr_version}") {
		exclude group: "dev.isxander"
	}

	modImplementation("nl.enjarai:cicada-lib:${project.cicada_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	//helpful mod for looking at the contrails
	modImplementation("maven.modrinth:camera-utils:${project.camera_utils_version}")

	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}